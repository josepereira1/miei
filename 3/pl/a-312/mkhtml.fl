%option noyywrap
%{
  #include <string.h>
 FILE * out, *abc;
 char* title; /*  vari√°vel global */
%}

%x ABC  META POEMA
%%

<*>title:\ [\*=#].*    {BEGIN META; fprintf(out,"<h1>%s</h1>\n",yytext+8);
                            title = strdup(yytext+8);}

<*>title:.*    {BEGIN META; fprintf(out,"<h1>%s</h1>\n",yytext+6);
                            title = strdup(yytext+6);} /* <*> significa qq contexto */

<*>\<abc\>     {BEGIN ABC; }
<META>{
 \n\n          { BEGIN POEMA; fprintf(out, "</br></br></br>");}
 ([^:\n]+)/:   {fprintf(out,"</br><b>%s</b>",yytext);}  /* tudo menos : ou \n */
}
<ABC>{
  \<\/abc\> {BEGIN POEMA; }
 .|\n      { fprintf(abc, "%s",yytext); }
}
<POEMA>{
  \n        { fprintf(out,"</br>");}  /* fazer as quebras de linha no html */
}

<*>.|\n        { fprintf(out,"%s",yytext);}  /* outra qq coisa vai para o out */
%%

void initHTML(FILE* file){
  fprintf(file,"<html>\n\t<head>\n\t\t<meta charset='UTF-8'/>\n\t</head>\n\t<body>\n\t\t<ul>\n"); 
}

void endHTML(FILE* file){
  fprintf(file,"\n\t\t</ul>\n\t</body>\n</html>\n");
}

int main(int argc, char* argv[]){
  char filename[200];
  FILE* ind = fopen ("index.html","w");
  abc = fopen ("cancioneiro.abc","w");
  if(argc>1){
   initHTML(ind);
   for(int i=1; i < argc; i++){
     sprintf(filename,"%s.html",argv[i]);

     yyin = fopen (argv[i],"r");
     out = fopen (filename,"w");
     yylex();

     fprintf(ind,"\t\t\t<li><a href='%s'>%s</a></li>\n",filename, title); 
     fclose(out); fclose(yyin); 
   }
   endHTML(ind); 
  } 
  else { return 1; }
  fclose(ind);
  fclose(abc);
  //system("abcp -q cancioneiro.abc");
  return 0;
}
